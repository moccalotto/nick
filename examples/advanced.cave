# Large initial sizes yields multi-room or
# complex caves with stalactites/columns, etc.
# Small initial sizes often yield simple caves
# Examples:
# 20x20: simple, one-room caves
# 60x20: complex one-to-two room caves
#        (depending on how much snow is added)

suggest export.type = iterm

set $width              between 25 and 70
set $height             between 25 and $width
set $initial_coverage   between 30% and 45%
set $min_area_size      between 1 and @[[ randint(ceil($height / 6)) ]]
set $egress_count       between 1 and @[[ randint(randint(randint(4))) + 1 ]] # number between 1 and 4, where 1 is the most likely
set $egress_size        between 2 and @[[ max(2, randint(ceil($height / 6))) ]]

background-file  assets/backgrounds/paper_by_darkwood67/old_paper_by_darkwood67.jpeg
wall-color       RGBA(0x44,0x44,0x44,0xcc)
grid-color       RGBA(0x44,0x44,0x66,0x55)
grid             $width x $height


init $width x $height

snow $initial_coverage

border 3 100%
border 4  30%
border 5  10%

# standard excavator
evolve B5678/S345678

loop $egress_count
    egress random $egress_size
endloop

# Remove tiny rooms/holes
prune-small-areas $min_area_size

# Scale up and excavate
loop 3
    scale 1.5
    # standard excavator
    evolve B5678/S345678
endloop

# Dig tunnels between all separated rooms
connect-rooms 3%

loop 1000
    # smooth edges
    evolve B5678/S5678

    scale 1.5

    # smooth edges
    evolve B5678/S5678

    if @cells > 2000000
        break-loop
    endif

    if @height > 960
        break-loop
    endif
endloop

set $currentHeight = @height

border @[[ $currentHeight / $height ]] 100% (off)
